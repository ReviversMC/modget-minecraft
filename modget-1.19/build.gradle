plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.3'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17


archivesBaseName = project.archives_base_name
def mod_version = project.mod_version as Object
version = "${mod_version}+1.19"

apply from: '../common.gradle'


dependencies {
    include implementation(project(path: ":modget-1.19-compat", configuration: "namedElements"))

    minecraft "com.mojang:minecraft:${project.minecraft_version_1_19}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings_1_19}:v2"

    // Required Fabric API modules
    modImplementation(fabricApi.module("fabric-api-base", project.fabric_api_version_1_19))
    modImplementation(fabricApi.module("fabric-command-api-v2", project.fabric_api_version_1_19))
    modImplementation(fabricApi.module("fabric-networking-api-v1", project.fabric_api_version_1_19))

    // ModMenu
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version_1_19}"
    // Cloth Config
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version_1_19}") {
        exclude(group: "net.fabricmc.fabric-api")
    }


    if (project.use_third_party_mods == "true") {
        configurations {
            modRuntimeOnly {
                transitive = true
                exclude module: "fabric-api-base"
                exclude module: "fabric-command-api-v2"
                exclude module: "fabric-networking-api-v1"
                exclude module: "commons-text"
            }
        }

        modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version_1_19}"
        modRuntimeOnly "maven.modrinth:lazydfu:${project.lazydfu_version_1_19}"
        modRuntimeOnly "maven.modrinth:sodium:${project.sodium_version_1_19}"
        runtimeOnly "org.joml:joml:${project.joml_version_sodium_1_19}"
        modRuntimeOnly "maven.modrinth:lithium:${project.lithium_version_1_19}"
        modRuntimeOnly "maven.modrinth:starlight:${project.starlight_version_1_19}"
        modRuntimeOnly "maven.modrinth:smoothboot-fabric:${project.smoothboot_version_1_19}"
        modRuntimeOnly "curse.maven:no-fade-452768:${project.no_fade_version_1_19}"
        modRuntimeOnly "fr.catcore:server-translations-api:${server_translations_version_1_19}"
    }
}
