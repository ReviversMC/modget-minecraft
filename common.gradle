group = project.maven_group as Object

configurations {
    includeTransitive {
        transitive = true
    }
    modRuntimeOnly {
        exclude module: "fabric-loader"
    }
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    mavenLocal()
    if (project.use_third_party_mods == "true") {
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }
        maven {
            url = "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }
    maven {
        url = "https://mvnrepository.com/"
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases"
    }
    maven {
        url = "https://maven.shedaniel.me/"
    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
    maven {
        name = "NucleoidMC"
        url = "https://maven.nucleoid.xyz/"
    }
    mavenCentral()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

    // Modget Manifest API
    implementation("com.github.ReviversMC.modget-manifest-api:compat:${project.modget_manifest_api_version}")
    implementation("com.github.ReviversMC.modget-manifest-api:spec3:${project.modget_manifest_api_version}")
    implementation("com.github.ReviversMC.modget-manifest-api:spec4:${project.modget_manifest_api_version}")
    // implementation(files("libs/modget-manifest-api-compat-0.1.0.jar"))
    // implementation(files("libs/modget-manifest-api-spec3-0.2.0.jar"))
    // implementation(files("libs/modget-manifest-api-spec4-0.1.0.jar"))
    implementation("com.fasterxml.jackson.core:jackson-core:${project.jackson_version}")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${project.jackson_version}")
    implementation("com.fasterxml.jackson.core:jackson-databind:${project.jackson_version}")
    implementation("com.fasterxml.jackson.core:jackson-annotations:${project.jackson_version}")
    implementation("org.yaml:snakeyaml:${project.snakeyaml_version}")

    // Modget Library
    implementation("com.github.ReviversMC:modget-lib:${project.modget_lib_version}")
    // implementation(files("../libs/modget-lib-${project.modget_lib_version}.jar"))

    implementation("org.apache.commons:commons-text:${project.commons_text_version}")

    compileOnly "com.github.spotbugs:spotbugs:${project.spotbugs_version}"
}


tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
}

test {
    useJUnitPlatform()
}



project.afterEvaluate {
    configurations.includeTransitive.incoming.resolutionResult.allComponents {
        if (it.id instanceof ModuleComponentIdentifier) {
            def that = it
            dependencies {
                include group: that.id.getGroup(), name: that.id.getModule(), version: that.id.getVersion()
            }
        }
    }
}

processResources {
    filesMatching("fabric.mod.json") {
        expand project.properties
    }
}


java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from "LICENSE"
}
